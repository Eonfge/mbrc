import java.text.SimpleDateFormat

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'idea'


def buildTime() {
  def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'Z'")
  df.setTimeZone(TimeZone.getTimeZone("UTC"))
  return df.format(new Date())
}

def gitHash() {
  return "git -C ${rootDir} rev-parse --short HEAD".execute().text.trim()
}

android {
  compileSdkVersion 23
  buildToolsVersion '23.0.3'

  defaultConfig {
    applicationId 'com.kelsos.mbrc'
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 105
    versionName "1.0.0-alpha.1"

    // Enabling multidex support.
    multiDexEnabled true

  }

  signingConfigs {
    release {
      storeFile file('placeholder')
      keyAlias "placeholder"
      storePassword "placeholder"
      keyPassword "placeholder"
    }
  }

  buildTypes {
    release {
      signingConfig signingConfigs.release
      debuggable false
      zipAlignEnabled true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

      buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
      buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    }

    debug {
      applicationIdSuffix ".dev"
      versionNameSuffix "-dev"
      buildConfigField "String", "GIT_SHA", "\"debug_build\""
      buildConfigField "String", "BUILD_TIME", "\"debug_build\""
    }

    preview {
      signingConfig signingConfigs.release
      debuggable false
      zipAlignEnabled true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      applicationIdSuffix ".preview"
      versionNameSuffix "-preview"

      buildConfigField "String", "GIT_SHA", "\"${gitHash()}\""
      buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
    }
  }


  productFlavors {

    development {
      minSdkVersion 21
    }

    production {
      // Default minSdkVersion
    }
  }

  packagingOptions {
    exclude 'META-INF/ASL2.0'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/services/javax.annotation.processing.Processor'
  }

  lintOptions {
    abortOnError false
    lintConfig file("lint.xml")
    xmlReport false
    htmlReport true
    htmlOutput file("$project.buildDir/reports/lint/lint-result.html")
    xmlOutput file("$project.buildDir/reports/lint/lint-result.xml")
  }

  applicationVariants.all { variant ->
    variant.outputs.each { output ->
      def originalFilename = output.outputFile
      def versionInfo = "-v${variant.mergedFlavor.versionCode}-${variant.mergedFlavor.versionName}"
      def filename = originalFilename.name
          .replace(".apk", versionInfo + ".apk")
          .replace(project.name, defaultConfig.applicationId);
      filename = "${originalFilename.parent}/${filename}"
      output.outputFile = file(filename)
    }
  }
  dexOptions {
    jumboMode true
    incremental false
    preDexLibraries false
    javaMaxHeapSize "2g"
  }

  sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    debug.java.srcDirs += 'src/debug/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
  }
}

idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.13'
}

ext {
  supportLibraryVersion = '23.3.0'
  jacksonVersion = '2.7.4'
  okhttpVersion = '3.2.0'
  retrofitVersion = '2.0.2'
  dbflow_version = "3.0.0-beta6"
}

dependencies {
  compile "com.android.support:support-v4:${supportLibraryVersion}"
  compile "com.android.support:appcompat-v7:${supportLibraryVersion}"
  compile "com.android.support:cardview-v7:${supportLibraryVersion}"
  compile "com.android.support:preference-v7:${supportLibraryVersion}"
  compile "com.android.support:preference-v14:${supportLibraryVersion}"
  compile "com.android.support:recyclerview-v7:${supportLibraryVersion}"
  compile "com.android.support:palette-v7:${supportLibraryVersion}"
  compile "com.android.support:support-annotations:${supportLibraryVersion}"
  compile "com.android.support:design:${supportLibraryVersion}"

  compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"

  compile "com.squareup.retrofit2:retrofit:${retrofitVersion}"
  compile "com.squareup.retrofit2:adapter-rxjava:${retrofitVersion}"
  compile "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"

  compile "com.squareup.picasso:picasso:2.5.2"

  compile "com.squareup.okhttp3:okhttp-urlconnection:${okhttpVersion}"
  compile "com.squareup.okhttp3:okhttp:${okhttpVersion}"
  compile "com.squareup.okhttp3:okhttp-ws:${okhttpVersion}"
  compile "com.squareup.okhttp3:logging-interceptor:${okhttpVersion}"

  compile 'io.reactivex:rxandroid:1.2.0'
  compile 'io.reactivex:rxjava:1.1.5'
  compile 'io.reactivex:rxkotlin:0.55.0'

  compile 'com.jakewharton:butterknife:8.0.1'
  kapt 'com.jakewharton:butterknife-compiler:8.0.1'

  compile 'com.jakewharton.timber:timber:4.1.2'
  compile 'com.android.support:multidex:1.0.1'

  compile "org.roboguice:roboguice:4.0.0"
  kapt 'org.roboguice:roboblender:4.0.1-SNAPSHOT'
  compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  kapt "com.github.Raizlabs.DBFlow:dbflow-processor:${dbflow_version}"

  // kapt for kotlin apt
  compile "com.github.Raizlabs.DBFlow:dbflow-core:${dbflow_version}"
  compile "com.github.Raizlabs.DBFlow:dbflow:${dbflow_version}"
  compile "com.github.Raizlabs.DBFlow:dbflow-kotlinextensions:${dbflow_version}"

  compile('com.afollestad.material-dialogs:core:0.8.5.8@aar') {
    transitive = true
  }

  kapt 'org.roboguice:roboblender:4.0.1-SNAPSHOT'
  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-core:2.0.52-beta'
  testCompile 'org.powermock:powermock-module-junit4:1.6.5'
  testCompile 'org.powermock:powermock-api-mockito:1.6.5'
  testCompile 'org.robolectric:robolectric:3.1-rc1'
  testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

  compile 'nl.littlerobots.rxlint:rxlint:1.0'

  debugCompile 'com.facebook.stetho:stetho:1.3.1'
  debugCompile 'com.facebook.stetho:stetho-okhttp3:1.3.1'
}

allprojects {
  afterEvaluate { project ->

    def signProperties = rootProject.file('signing.properties')
    if (signProperties.exists()) {
      def prop = new Properties()
      prop.load(new FileInputStream(signProperties))
      def filepath = prop.getAt("keys")
      def keyProperties = file(filepath)
      if (keyProperties.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(keyProperties))
        def keystorePath = props.getAt('keystore')
        def releaseConfig = android.signingConfigs.getAt('release')
        releaseConfig.storeFile = file(keystorePath)
        releaseConfig.storePassword = props.getAt('storePass')
        releaseConfig.keyAlias = props.getAt('keyAlias')
        releaseConfig.keyPassword = props.getAt('keyPass')
      }
    }
  }
}

project.tasks.withType(JavaCompile) {
  task -> options.compilerArgs << "-AguiceAnnotationDatabasePackageName=com.kelsos.mbrc"
}

kapt {
  generateStubs = true
}
