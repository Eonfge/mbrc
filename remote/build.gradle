import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
        maven {url 'https://oss.sonatype.org/content/repositories/snapshots/'}
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.8.+'
    }
}

apply plugin: 'android'

repositories {
    maven { url "http://dl.bintray.com/populov/maven" }
    maven { url 'http://dl.bintray.com/kelsos/maven' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    mavenCentral()
    mavenLocal()
}

def buildTime() {
    def df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm'UTC'")
    df.setTimeZone(TimeZone.getTimeZone("UTC"))
    return df.format(new Date())
}

def gitSha() {
    return "git -C ${rootDir} rev-parse --short HEAD".execute().text.trim()
}


def versionMajor = 0
def versionMinor = 9
def versionPatch = 9

dependencies {
    compile 'com.android.support:support-v4:19.0.1'
    compile 'com.android.support:appcompat-v7:19.0.1'
    compile 'org.roboguice:roboguice:2.0'
    compile 'com.squareup:otto:1.3.4'
    compile 'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
    compile 'com.viewpagerindicator:library:2.4.1@aar'
    compile 'com.mobeta.android.dslv:drag-sort-listview:0.6.1@aar'
    compile 'com.squareup.picasso:picasso:2.1.1'
    compile 'de.keyboardsurfer.android.widget:crouton:1.8.3@aar'
    compile 'com.google.inject:guice:3.0:no_aop'
    compile 'com.github.manuelpeinado.fadingactionbar:fadingactionbar-abc:3.1.0'

    instrumentTestCompile 'junit:junit:4.10'
    instrumentTestCompile 'org.robolectric:robolectric:2.3-SNAPSHOT'
}

android {
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 19
    buildToolsVersion "19.0.1"

    defaultConfig {
        minSdkVersion 10
        targetSdkVersion 19
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\";"
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\";"
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode 85
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    sourceSets {
        instrumentTest.setRoot('src/test')
    }

    signingConfigs {
        release {
            storeFile file('placeholder')
            keyAlias "placeholder"
            storePassword "placeholder"
            keyPassword "placeholder"
        }
        debug {}
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            zipAlign true
        }
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix ".debug"
        }
    }

    productFlavors {
        free {}
    }

    lintOptions {
        abortOnError false
        xmlReport false
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.10'
}


apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'sonar-runner'

checkstyle {
    configFile = file("$rootDir/config/quality/checkstyle/checkstyle.xml")
}

task checkstyle(type: Checkstyle) {
    source 'src/main/java'
    include '**/*.java'
    exclude '**/gen/**'
    classpath = files(project.configurations.compile.asPath)

}

task findbugs(type: FindBugs) {
    ignoreFailures = true
    excludeFilter file("$rootDir/config/quality/findbugs/findbugs-filter.xml")
    classes = fileTree('build/classes/free/debug/')
    source = fileTree('src/main/java/')
    classpath = files( project.configurations.compile.asPath )
    effort = 'max'
}

task pmd(type: Pmd) {
    ruleSetFiles = files("$rootDir/config/quality/pmd/pmd-ruleset.xml")
    ruleSets = ["basic", "braces", "strings"]
    source = fileTree(android.sourceSets.main.java.srcDirs)
}

gradle.taskGraph.afterTask {Task task, TaskState state ->
    if(state.failure) {
        if (task.name in ['checkstyle']) {
            ant.xslt(in: "$buildDir/reports/checkstyle/checkstyle.xml",
                    style: new File(rootDir, 'config/quality/checkstyle/checkstyle-noframes-sorted.xsl'),
                    out: new File("$buildDir/reports/checkstyle/checkstyle.html"))
        }
    }
}

allprojects {
    afterEvaluate { project ->
        def propsFile = rootProject.file('signing.properties')
        def configName = 'release'

        if (propsFile.exists()) {
            def prop = new Properties()
            prop.load(new FileInputStream(propsFile))
            def filepath = prop['keys']
            def keyPropFile = file(filepath)
            if (keyPropFile.exists() && android.signingConfigs.hasProperty(configName)) {
                def props = new Properties()
                props.load(new FileInputStream(keyPropFile))
                def keystorePath = props['keystore']
                android.signingConfigs[configName].storeFile = file(keystorePath)
                android.signingConfigs[configName].storePassword = props['storePass']
                android.signingConfigs[configName].keyAlias = props['keyAlias']
                android.signingConfigs[configName].keyPassword = props['keyPass']
            }
        }
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", "http://localhost:9000"
        property "sonar.jdbc.url", "jdbc:h2:tcp://localhost:9092/sonar"
        property "sonar.jdbc.driverClassName", "org.h2.Driver"
        property "sonar.jdbc.username", "sonar"
        property "sonar.jdbc.password", "sonar"
        property "sonar.branch", "gradle"
        property "sonar.dynamicAnalysis", "reuseReports"
        properties["sonar.sources"] = android.sourceSets.main.java.srcDirs
        properties["sonar.tests"] = android.sourceSets.instrumentTest.java.srcDirs
        properties["sonar.binaries"] = file("build/classes/free/debug")
        property "sonar.surefire.reportsPath","build/instrumentTest-results/connected/"
    }
}